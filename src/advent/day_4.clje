(ns advent.day-4
  (:require [advent.utils :as u]))

;; - It is a six-digit number.
;; - The value is within the range given in your puzzle input.
;; - Two adjacent digits are the same (like 22 in 122345).
;; - Going from left to right, the digits never decrease; they
;;   only ever increase or stay the same (like 111123 or 135679).

(def input [138241 674034])

(defn explode-num
  [n]
  (-> n str seq))

(defn never-decreasing?
  [digits]
  (= (sort digits) digits))

(defn equal-adjacent?
  [digits]
  (->> digits
       (partition 2 1)
       (some (fn [[x y]] (= x y)))))

(defn part-one
  ([]
   (part-one input))
  ([[lower higher]]
   (filter (comp (every-pred never-decreasing?
                             equal-adjacent?)
                 explode-num)
           (range lower (inc higher)))))

(defn equal-adjacent-only-two?
  [digits]
  (loop [[x & xs] (rest digits)
         current (first digits)
         n 1]
    (cond
      (not x) (= n 2)
      (= x current) (recur xs x (inc n))
      (> n 1) (if (= n 2)
                true
                (recur xs x 1))
      :else (recur xs x 1))))

(defn part-two
  ([]
   (part-two input))
  ([[lower higher]]
   (filter (comp (every-pred never-decreasing?
                             equal-adjacent-only-two?)
                 explode-num)
           (range lower (inc higher)))))
