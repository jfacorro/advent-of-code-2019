(ns advent.day-14
  (:require [advent.utils :as u]
            [clojure.string :as str]))

;; The solutions to this day is an implementation in Clojure of the solutions
;; found here: https://github.com/gboyegadada/algos/blob/master/AoC19/day14.py

;; -----------------------------------------------------------------------------
;; Part One
;; -----------------------------------------------------------------------------

(defn parse-quantity-name
  [s]
  (let [[quantity name] (str/split s #" ")]
    [name (erlang/binary_to_integer quantity)]))

(defn line->reaction
  [line]
  (let [[input output] (str/split line #"=>")
        input  (->> (str/split input #",")
                    (map str/trim)
                    (map parse-quantity-name)
                    (reduce conj {}))
        [name quantity] (-> (str/trim output)
                            parse-quantity-name)]
    {name {:quantity quantity
           :input input}}))

(defn quantity
  [reactions name fuel]
  (if (= name "FUEL")
    fuel
    (->> reactions
         (filter #(-> % second :input (get name)))
         (reduce (fn [q [parent info]]
                   (-> (quantity reactions parent fuel)
                       (/ (:quantity info))
                       math/ceil
                       (* (-> info :input (get name)))
                       (+ q)))
                 0)
         erlang/trunc)))

(defn input->reactions
  [input]
  (->> input
       (map line->reaction)
       (reduce merge {})))

(defn part-one
  ([] (-> (u/input-for-day 14)
          input->reactions
          part-one))
  ([reactions]
   (quantity reactions "ORE")))

;; -----------------------------------------------------------------------------
;; Part Two
;; -----------------------------------------------------------------------------

(def trillion 1000000000000)

(defn part-two
  ([] (-> (u/input-for-day 14)
          input->reactions
          part-two))
  ([reactions]
   (loop [lower 1
          higher trillion]
     (if (>= 1 (- higher lower))
       lower
       (let [middle (erlang/div (+ lower higher) 2)
             q (quantity reactions "ORE" middle)]
         (if (<= q trillion)
           (recur middle higher)
           (recur lower middle)))))))
